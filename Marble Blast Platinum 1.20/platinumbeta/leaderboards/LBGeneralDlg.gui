//--- OBJECT WRITE BEGIN ---
new GuiControl(LBGeneralDlg) {
   profile = "GuiDefaultProfile";
   horizSizing = "width";
   vertSizing = "height";
   position = "0 0";
   extent = "640 480";
   minExtent = "8 8";
   visible = "1";
   helpTag = "0";

   new GuiControl() {
      profile = "GuiDefaultProfile";
      horizSizing = "width";
      vertSizing = "height";
      position = "0 0";
      extent = "640 480";
      minExtent = "8 8";
      visible = "1";
      helpTag = "0";

      new GuiBitmapCtrl() {
         profile = "GuiDefaultProfile";
         horizSizing = "center";
         vertSizing = "center";
         position = "99 23";
         extent = "442 434";
         minExtent = "8 8";
         visible = "1";
         helpTag = "0";
         bitmap = "./marbleSelect/marblebox";
         wrap = "0";

         new GuiMLTextCtrl(LB_GeneralScoresText) {
            profile = "GuiMLTextProfile";
            horizSizing = "right";
            vertSizing = "bottom";
            position = "17 19";
            extent = "406 319";
            minExtent = "8 8";
            visible = "1";
            helpTag = "0";
            lineSpacing = "2";
            allowColorChars = "0";
            maxChars = "-1";
         };
         new GuiBitmapButtonCtrl(LB_GeneralPrev) {
            profile = "GuiDefaultProfile";
            horizSizing = "right";
            vertSizing = "top";
            position = "24 346";
            extent = "116 54";
            minExtent = "8 8";
            visible = "1";
            command = "LBGeneralDlg.prev();";
            helpTag = "0";
            text = "Prev";
            groupNum = "-1";
            buttonType = "RepeaterButton";
            repeatPeriod = "450";
            repeatDecay = "0.958";
            bitmap = "./marbleSelect/prev";
         };
         new GuiBitmapButtonCtrl(LB_GeneralNext) {
            profile = "GuiDefaultProfile";
            horizSizing = "left";
            vertSizing = "top";
            position = "300 346";
            extent = "116 54";
            minExtent = "8 8";
            visible = "1";
            command = "LBGeneralDlg.next();";
            helpTag = "0";
            text = "Next";
            groupNum = "-1";
            buttonType = "RepeaterButton";
            repeatPeriod = "450";
            repeatDecay = "0.958";
            bitmap = "./marbleSelect/next";
         };
         new GuiBitmapButtonCtrl() {
            profile = "GuiDefaultProfile";
            horizSizing = "center";
            vertSizing = "top";
            position = "163 346";
            extent = "116 54";
            minExtent = "8 8";
            visible = "1";
            command = "Canvas.popDialog(LBGeneralDlg);";
            helpTag = "0";
            text = "Okay";
            groupNum = "-1";
            buttonType = "PushButton";
            repeatPeriod = "1000";
            repeatDecay = "1";
            bitmap = "./marbleSelect/okay";
         };
      };
   };
};
//--- OBJECT WRITE END ---

function LBGeneralDlg::onWake(%this) {
   %this.reset();
   %this.loadData();
   %this.updatePage();
}

function LBGeneralDlg::onSleep(%this) {
   %this.reset();
}

function LBGeneralDlg::reset(%this) {
   for (%i = 0; %i < %this.scores; %i ++) {
      %this.score["rating", %i] = "";
      %this.score["user", %i] = "";
   }
   %this.scores = 0;
   %this.page = 0;
   %this.loading = false;
}

function LBGeneralDlg::next(%this) {
   %this.page ++;
   %this.updatePage();
}

function LBGeneralDlg::prev(%this) {
   %this.page --;
   %this.updatePage();
}

function LBGeneralDlg::updatePage(%this) {
   LB_GeneralPrev.setActive(true);
   LB_GeneralNext.setActive(true);
   if (%this.page <= 0) {
      %this.page = 0;
      LB_GeneralPrev.setActive(false);
   } else if (%this.page >= 124) {
      %this.page = 124;
      LB_GeneralNext.setActive(false);
   }
   %this.showPage();
}

function LBGeneralDlg::showPage(%this) {
   if (%this.loading) {
      %text = "<spush><font:DomCasualD:24><just:center>Please Wait...";
      LB_GeneralScoresText.setText(%text);
      return;
   }
   %title = "General Rankings " @ (%this.page * 20) + 1 @ " - " @ (%this.page * 20) + 20;
   %text = "<spush><font:DomCasualD:24><just:center>" @ %title @ "<spop>\n<just:left><shadow:1:1><color:000000><shadowcolor:ffffff4f><tab:34,340><font:Arial Bold:14>Place\t\Player\t<just:right>Rating<just:left>";
   for (%i = 1; %i <= 20; %i ++) {
      %text = %text NL "<spush>" @ LBSpecialColor(%i + (%this.page * 20), true) @ %i + (%this.page * 20) TAB clipPx("<font:Arial Bold:14>" @ %this.score["user", %i + (%this.page * 20)], 300, true) TAB %this.score["rating", %i + (%this.page * 20)] @ "<spop>";
   }
   LB_GeneralScoresText.setText(%text);
}

function LBGeneralDlg::fixScores(%this) {
   for (%i = 0; %i <= 2500; %i ++) {
      if (%this.score["user", %i] $= "")
         %this.score["user", %i] = decodeName($LB::DefaultHSName);
      if (%this.score["rating", %i] $= "")
         %this.score["rating", %i] = "0";
   }
}

function LBGeneralDlg::loadData(%this) {
   if (!isObject(LBGeneralNetwork))
      new TCPObject(LBGeneralNetwork);
   %server = $LB::server;
   %page = $LB::serverPath @ "generalscores.php";
   %query = LBDefaultQuery() @ "&thousand=true&start=0";
   LBGeneralNetwork.post(%server,%page,%query);

   %this.loading = true;
}

function LBGeneralNetwork::onLine(%this,%line) {
   Parent::onLine(%this,%line);
   %this.parseSigs(%line);
   if (getWord(%line,0) $= "SIG") {
      switch (getWord(%line,1)) {
         case 1: //HiGuy: page finish
            %this.echo("Finish");
            //%this.disconnect();
            LBGeneralDlg.loading = false;
            LBGeneralDlg.fixScores();
            LBGeneralDlg.showPage();
         case 6: //HiGuy: invalid username/password
            %this.echo("Invalid Username or Password");
            LBAssert("Error!","Invalid username or password, please try again!","");
            //%this.disconnect();
         case 16:
            LBGeneralDlg.reset();
      }
   } else if (getWord(%line,0) $= "SCORE") { // HiGuy: Scores!
      %user = decodeName(getWord(%line, 1));
      %rating = decodeName(getWord(%line, 2));
      %place = decodeName(getWord(%line, 3));

      LBGeneralDlg.score["user", %place] = %user;
      LBGeneralDlg.score["rating", %place] = %rating;

      LBGeneralDlg.scores = max(LBGeneralDlg.scores, %place);
      //HiGuy: *Incase* we have a blankâ€”
   }
}
